# Comando de Inicio: empezar
para empezar
  leelista [¿Qué dificultad quieres?] "n
  dificultad :n
fin

para dificultad :n
  botones
  repite :n
   [ haz "x (suma (-8) azar 35)*15
     haz "y (suma (-8) azar 35)*15
     piedra  :x :y ]
  sl ponxy 350 200 poncl azuloscuro circulo 50 rellenazona
  centro
fin

para botones
  borrapantalla
  si no miembro? "Angulo listavars
     [ haz "Angulo 90 ]
  si no miembro? "Avance listavars
     [ haz "Avance 10 ]
# 
  botonigu "Av "Avanza
  botonigu "Re "Retrocede
  botonigu "Gd "Gira\ derecha
  botonigu "Gi "Gira\ izquierda
#
  posicionigu "Av [-350 25]
  posicionigu "Re [-360 -25]
  posicionigu "Gd [-300 0]
  posicionigu "Gi [-440 0]
#
  accionigu "Av [juega 10]
  accionigu "Re [juega -10]
  accionigu "Gd [giraderecha :Angulo ]
  accionigu "Gi [giraizquierda :Angulo ]
#
  dibujaigu "Av
  dibujaigu "Re
  dibujaigu "Gd
  dibujaigu "Gi
fin

para piedra :x :y
  sl ponxy :x :y
  bl circulo 15 rellenazona
  sl centro
fin

para juega :paso
  avanza :paso
  haz "color encuentracolor posicion
  si iguales? :color [0 0 128]
   [ mensaje [¡Muy bien! ¡Has llegado!]
     poninstrumento 0
     borrasecuencia partitura tocamusica
     centro ]
   [ si iguales? :color [0 0 0]
     [ mensaje [¡Ay! ¡He chocado con una piedra!]
       centro ] ]
fin

para partitura
 # crea la secuencia de notas: Supercalifragilisticoespialidoso
  sec [ 0.25 sol sol sol sol  la sol sol mi sol sol la sol 0.5 sol fa ]
  sec [ 0.25 sol sol sol sol  la sol sol mi sol sol la sol 0.5 sol mi ]
  sec [ 0.25 sol sol sol sol  la sol sol sol :+ do do re do 0.5 do :-  la  ]
  sec [ 0.25 la :+do :- si la :+ do :- sol sol  mi sol sol la si :+ 0.5 do 1 do :-] 
fin
